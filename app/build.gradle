plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.future.tailormade"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    hilt {
        enableTransformForLocalTests = true
    }

    kapt {
        correctErrorTypes true
    }

    kotlinOptions {
        jvmTarget = versions.jvm
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    sourceSets {
        androidTest.assets.srcDirs +=
                files("$projectDir/schemas".toString())
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", ".aar"])

    // Libraries
    api project(":libraries:tailormade_base")
    implementation project(":libraries:tailormade_router")

    // Features
    implementation project(":features:tailormade_auth")
    implementation project(":features:tailormade_profile")

    // Location
    implementation gms.location
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // Tests
    testImplementation test.junit
    testImplementation test.androidx_core
    testImplementation test.mockito
    androidTestImplementation androidTest.junit
    androidTestImplementation androidTest.espresso

    // Paging
    testImplementation paging.test_common

    // Annotation Processing
    kapt dataBinding.kapt

    // Glide
    kapt glide.kapt

    implementation materialDesign

    // Dagger 2
    kapt dagger.kapt_processor
    kapt dagger.kapt_compiler

    // Dagger Hilt
    implementation daggerHilt.core
    implementation daggerHilt.lifecycle_viewmodel
    kapt daggerHilt.kapt_compiler
    kapt daggerHilt.kapt_google_compiler
    kaptTest daggerHilt.kaptTest_compiler
    testImplementation daggerHilt.test

    // Room
    kapt room.kapt_compiler
    androidTestImplementation room.androidTest
    androidTestImplementation room.androidTest_arch_persistence
}